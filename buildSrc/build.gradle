plugins {
    id 'org.jetbrains.kotlin.jvm' version("1.7.21")
    id 'org.gradle.java-gradle-plugin'
}

repositories {
    mavenLocal()

    project.properties.getOrDefault("mavenInternalRepositoryUrl", "").tap { localUrl ->
        if (localUrl.empty) {
            project.logger.error("Using mavenCentral repository")
            mavenCentral()
        } else {
            project.logger.error("Using internal URL repository: ${localUrl}")
            maven {
                url localUrl

                credentials {
                    username = project.properties["mavenUsername"]
                    password = project.properties["mavenPassword"]
                }
            }
        }
    }
}



dependencies {
    implementation group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib'
    implementation group: 'org.jetbrains.kotlin', name: 'kotlin-test'
    implementation group: 'org.jetbrains.kotlin', name: 'kotlin-gradle-plugin'

    implementation group: 'org.jetbrains.dokka', name: 'dokka-gradle-plugin', version: '0.10.0'
    implementation group: 'org.eclipse.jgit', name: 'org.eclipse.jgit', version: '5.8.1.202007141445-r'
    implementation group: 'com.auth0', name: 'java-jwt', version: '3.10.3'
    implementation group: 'com.github.oshi', name: 'oshi-core', version: '3.13.0'

    implementation group: 'org.reflections', name: 'reflections', version: '0.10.2'
    implementation group: 'org.apache.commons', name: 'commons-text', version: '1.11.0'

    implementation group: 'com.github.inforion.common', name: 'extensions', version: '0.4.8'
    implementation group: 'com.github.inforion.common', name: 'gson-json', version: '0.4.8'

    testImplementation group: 'junit', name: 'junit', version: '4.13'
}

gradlePlugin {
    plugins {
        create("gradle-build-config-plugin") {
            id = "ru.inforion.lab403.gradle.buildConfig"
            implementationClass = "ru.inforion.lab403.gradle.buildConfig.BuildConfigPlugin"
        }

        create("gradle-version-config-plugin") {
            id = "ru.inforion.lab403.gradle.versionConfig"
            implementationClass = "ru.inforion.lab403.gradle.versionConfig.VersionConfigPlugin"
        }

        create("gradle-dokka-mutltilang-plugin") {
            id = "ru.inforion.lab403.gradle.dokkaMultilang"
            implementationClass = "ru.inforion.lab403.gradle.dokkaMultilang.DokkaMultilangPlugin"
        }

        create("gradle-kopycat-plugin") {
            id = "ru.inforion.lab403.gradle.kopycat"
            implementationClass = "ru.inforion.lab403.gradle.kopycat.KopycatPlugin"
        }


    }
}



compileKotlin {
    kotlinOptions {
        jvmTarget = "11"
        freeCompilerArgs += ['-Xopt-in=kotlin.contracts.ExperimentalContracts']
    }
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = "11"
        freeCompilerArgs += ['-Xopt-in=kotlin.contracts.ExperimentalContracts']
    }
}
